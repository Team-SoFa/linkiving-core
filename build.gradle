plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'checkstyle'
    id 'jacoco'
}

group = 'com.sofa'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    //Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Database
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'org.postgresql:postgresql'

    // Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.11'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    //redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-hibernate5-jakarta'

    // Security
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation "org.springframework.security:spring-security-test"

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.13.0'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.13.0'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.13.0'

    // Spring Cloud OpenFeign
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    testImplementation "org.springframework.cloud:spring-cloud-starter-openfeign"
    testImplementation "com.squareup.okhttp3:mockwebserver:4.12.0"

    // Jsoup (HTML parsing for OG tag crawling)
    implementation 'org.jsoup:jsoup:1.18.3'

    // WebSocket
    implementation 'org.springframework.boot:spring-boot-starter-websocket'

    // WebFlux
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    //s3
    implementation platform("io.awspring.cloud:spring-cloud-aws-dependencies:3.1.1")
    implementation 'io.awspring.cloud:spring-cloud-aws-starter-s3'
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2025.0.0"
    }
}

checkstyle {
    maxWarnings = 0
    configFile = file("${rootDir}/codestyle/naver-checkstyle-rules.xml")
    configProperties = ["suppressionFile": "${rootDir}/codestyle/naver-checkstyle-suppressions.xml"]
    toolVersion = "8.42"
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/*Dto.class',
                    '**/*Dto$*.class',
                    '**/*Request.class',
                    '**/*Request$*.class',
                    '**/*Response.class',
                    '**/*Response$*.class',
                    '**/*Req.class',
                    '**/*Req$*.class',
                    '**/*Res.class',
                    '**/*Res$*.class',
                    '**/config/**',
                    '**/configuration/**',
                    '**/exception/**',
                    '**/*Application.class',
                    '**/Q*.class',
                    '**/entity/**',
                    '**/domain/**/*Entity.class',
                    '**/domain/link/util/OgTagCrawler.class',
                    "**/security/auth/**"
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport

    violationRules {
        rule {
            enabled = true
            element = 'CLASS'

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.30  // 30%부터 시작, 점진적으로 0.50 -> 0.70으로 상향
            }
        }
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/*Dto.class',
                    '**/*Dto$*.class',
                    '**/*Request.class',
                    '**/*Request$*.class',
                    '**/*Response.class',
                    '**/*Response$*.class',
                    '**/*Req.class',
                    '**/*Req$*.class',
                    '**/*Res.class',
                    '**/*Res$*.class',
                    '**/config/**',
                    '**/configuration/**',
                    '**/exception/**',
                    '**/*Application.class',
                    '**/Q*.class',
                    '**/entity/**',
                    '**/domain/**/*Entity.class',
                    '**/domain/link/util/OgTagCrawler.class',
                    "**/security/auth/**"
            ])
        }))
    }
}
